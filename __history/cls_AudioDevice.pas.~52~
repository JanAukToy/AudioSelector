// *****************************************************************************
// Created by JanAukToy
// [Github] https://github.com/JanAukToy
// *****************************************************************************
unit cls_AudioDevice;

interface

uses
  System.SysUtils, System.Classes, System.Generics.Collections,
  Winapi.Windows, Winapi.ActiveX, Winapi.PropSys,

  JAT.MMDeviceAPI, JAT.EndpointVolume;

type
  // Changed Valud Event
  TOnControlChangeNotify = procedure(const a_Data
    : AUDIO_VOLUME_NOTIFICATION_DATA) of object;

  // Audio Endpoint Volume Callbak Handler
  TAudioEndpointVolumeCallback = class(TInterfacedObject,
    IAudioEndpointVolumeCallback)
  private
    f_OnControlChangeNotify: TOnControlChangeNotify;
  public
    constructor Create(const a_OnControlChangeNotify: TOnControlChangeNotify);
    destructor Destroy; override;
    function OnNotify(pNotify: AUDIO_VOLUME_NOTIFICATION_DATA)
      : HRESULT; stdcall;
  end;

  // Audio Device Class
  TAudioDevice = class
  private
    f_Initialized: Boolean;

    f_AudioEndpointVolumeCallback: TAudioEndpointVolumeCallback;
    f_PropertyStore: IPropertyStore;

    f_Device: IMMDevice;
    f_AudioEndpointVolume: IAudioEndpointVolume;
    f_InterfaceFriendlyName: string;
    f_DeviceDesc: string;
    f_FriendlyName: string;
    f_InstanceId: string;
    f_ContainerId: TGUID;

    f_ChannelCount: Cardinal;
    f_MasterLevel: Single;
    f_ChannelLevelList: TList<Single>;
    f_Mute: Boolean;
    f_Step: Cardinal;
    f_StepCount: Cardinal;
    f_Min: Single;
    f_Max: Single;
    f_Spin: Single;

    function GetChannelLevel(const a_Index: Cardinal): Single;

    procedure SetInterfaceFriendlyName(const a_Value: string);
    procedure SetDeviceDesc(const a_Value: string);
    procedure SetFriendlyName(const a_Value: string);

    function GetDeviceProps(const a_PropertyStore: IPropertyStore): Boolean;
    function GetAudioEndpointVolumeProps(): Boolean;
    procedure OnControlChangeNotify(const a_Data
      : AUDIO_VOLUME_NOTIFICATION_DATA);
  public
    constructor Create(const a_Device: IMMDevice);
    destructor Destroy; override;

    property Initialized: Boolean read f_Initialized;

    property InterfaceFriendlyName: string read f_InterfaceFriendlyName
      write SetInterfaceFriendlyName;
    property DeviceDesc: string read f_DeviceDesc write SetDeviceDesc;
    property FriendlyName: string read f_FriendlyName write SetFriendlyName;
    property InstanceId: string read f_InstanceId;
    property ContainerId: TGUID read f_ContainerId;

    property ChannelCount: Cardinal read f_ChannelCount;
    property MasterLevel: Single read f_MasterLevel;
    property ChannelLevel[const a_Index: Cardinal]: Single read GetChannelLevel;
    property Mute: Boolean read f_Mute;
    property Step: Cardinal read f_Step;
    property StepCount: Cardinal read f_StepCount;
    property Min: Single read f_Min;
    property Max: Single read f_Max;
    property Spin: Single read f_Spin;
  end;

implementation

{ TIAudioEndpointVolumeCallback }

// *****************************************************************************
// Constructor
constructor TAudioEndpointVolumeCallback.Create(const a_OnControlChangeNotify
  : TOnControlChangeNotify);
begin
  inherited Create;

  f_OnControlChangeNotify := a_OnControlChangeNotify;
end;

// *****************************************************************************
// Destructor
destructor TAudioEndpointVolumeCallback.Destroy;
begin
  inherited;
end;

// *****************************************************************************
// Callback
function TAudioEndpointVolumeCallback.OnNotify
  (pNotify: AUDIO_VOLUME_NOTIFICATION_DATA): HRESULT;
begin
  f_OnControlChangeNotify(pNotify);

  Result := S_OK;
end;

{ TAudioDevice }

// *****************************************************************************
// Constructor
constructor TAudioDevice.Create(const a_Device: IMMDevice);
var
  l_Id: PWideChar;
  l_PointAudioEndpointVolume: Pointer;
begin
  // Init Variable
  f_Initialized := False;

  // Store Device
  f_Device := a_Device;

  // Create Channel Level List
  f_ChannelLevelList := TList<Single>.Create;

  // Get Device ID
  if Succeeded(a_Device.GetId(l_Id)) then
  begin
    // Store ID
    f_InstanceId := l_Id;

    // Get Open Property Interface
    if Succeeded(a_Device.OpenPropertyStore(STGM_READ, f_PropertyStore)) then
    begin
      // Get Device Properties
      if GetDeviceProps(f_PropertyStore) then
      begin
        // Get Audio Endpoint Volume Pointer Interface
        if Succeeded(a_Device.Activate(IID_IAudioEndpointVolume,
          CLSCTX_INPROC_SERVER, nil, l_PointAudioEndpointVolume)) then
        begin
          // Cast to Audio Endpoint Volume
          f_AudioEndpointVolume := IAudioEndpointVolume
            (l_PointAudioEndpointVolume) as IAudioEndpointVolume;

          // Create Callback Class
          f_AudioEndpointVolumeCallback := TAudioEndpointVolumeCallback.Create
            (OnControlChangeNotify);

          // Register Changed Value Callback
          if Succeeded(f_AudioEndpointVolume.RegisterControlChangeNotify
            (f_AudioEndpointVolumeCallback)) then
          begin
            // Get Audio Endpoint Volume Properties
            f_Initialized := GetAudioEndpointVolumeProps;
          end;
        end;
      end;
    end;
  end;
end;

// *****************************************************************************
// Destructor
destructor TAudioDevice.Destroy;
begin
  if Assigned(f_AudioEndpointVolumeCallback) then
  begin
    // Unregister Callback
    f_AudioEndpointVolume.UnregisterControlChangeNotify
      (f_AudioEndpointVolumeCallback);

    // Auto Destroying by the Above.
  end;

  // Destroy Channel Level List
  FreeAndNil(f_ChannelLevelList);

  inherited;
end;

// *****************************************************************************
// Get Channel Level
function TAudioDevice.GetChannelLevel(const a_Index: Cardinal): Single;
begin
  if a_Index < Cardinal(f_ChannelLevelList.Count) then
  begin
    Result := f_ChannelLevelList[a_Index];
  end
  else
  begin
    Result := 0;
  end;
end;

// *****************************************************************************
// Set InterfaceFriendlyName
procedure TAudioDevice.SetInterfaceFriendlyName(const a_Value: string);
var
  l_Variant: TPropVariant;
begin
  l_Variant.pwszVal := PWideChar(a_Value);
  f_PropertyStore.SetValue(PKEY_DeviceInterface_FriendlyName, l_Variant);
end;

// *****************************************************************************
// Set DeviceDesc
procedure TAudioDevice.SetDeviceDesc(const a_Value: string);
var
  l_Variant: TPropVariant;
begin
  l_Variant.pwszVal := PWideChar(a_Value);
  f_PropertyStore.SetValue(PKEY_Device_DeviceDesc, l_Variant);
end;

// *****************************************************************************
// Set FriendlyName
procedure TAudioDevice.SetFriendlyName(const a_Value: string);
var
  l_Variant: TPropVariant;
begin
  l_Variant.pwszVal := PWideChar(a_Value);
  f_PropertyStore.SetValue(PKEY_Device_FriendlyName, l_Variant);
end;

// *****************************************************************************
// Get Device Properties
function TAudioDevice.GetDeviceProps(const a_PropertyStore
  : IPropertyStore): Boolean;
var
  l_PropInterfaceFriendlyName: TPropVariant;
  l_PropDeviceDesc: TPropVariant;
  l_PropFriendlyName: TPropVariant;
  l_PropContainerId: TPropVariant;
begin
  Result := False;

  // Get All Properties
  // [ PKEY_Device_InstanceId ] is IMMDevice::GetId Value
  if (Succeeded(a_PropertyStore.GetValue(PKEY_DeviceInterface_FriendlyName,
    l_PropInterfaceFriendlyName))) and
    (Succeeded(a_PropertyStore.GetValue(PKEY_Device_DeviceDesc,
    l_PropDeviceDesc))) and
    (Succeeded(a_PropertyStore.GetValue(PKEY_Device_FriendlyName,
    l_PropFriendlyName))) and
    (Succeeded(a_PropertyStore.GetValue(PKEY_Device_ContainerId,
    l_PropContainerId))) then
  begin
    // Store Properties
    f_InterfaceFriendlyName := l_PropInterfaceFriendlyName.pwszVal;
    f_DeviceDesc := l_PropDeviceDesc.pwszVal;
    f_FriendlyName := l_PropFriendlyName.pwszVal;
    f_ContainerId := l_PropContainerId.puuid^;

    Result := True;
  end;
end;

// *****************************************************************************
// Get Auddio Endpoint Volume Properties
function TAudioDevice.GetAudioEndpointVolumeProps: Boolean;
var
  ii: Cardinal;
  l_ChannelCount: Cardinal;
  l_MasterLevel: Single;
  l_ChannelLevel: Single;
  l_ChannelLevelList: TList<Single>;
  l_Mute: LongBool;
  l_Step: Cardinal;
  l_StepCount: Cardinal;
  l_Min: Single;
  l_Max: Single;
  l_Spin: Single;
begin
  Result := False;

  // Get All Properties
  if (Succeeded(f_AudioEndpointVolume.GetChannelCount(l_ChannelCount))) and
    (Succeeded(f_AudioEndpointVolume.GetMasterVolumeLevelScalar(l_MasterLevel)))
    and (Succeeded(f_AudioEndpointVolume.GetMute(l_Mute))) and
    (Succeeded(f_AudioEndpointVolume.GetVolumeStepInfo(l_Step, l_StepCount)))
    and (Succeeded(f_AudioEndpointVolume.GetVolumeRange(l_Min, l_Max, l_Spin)))
  then
  begin
    l_ChannelLevelList := TList<Single>.Create;

    try
      // Get All Channel Volume
      for ii := 0 to l_ChannelCount - 1 do
      begin
        if (Succeeded(f_AudioEndpointVolume.GetChannelVolumeLevelScalar(ii,
          l_ChannelLevel))) then
        begin
          // Add Value
          l_ChannelLevelList.Add(l_ChannelLevel);
        end;
      end;

      // Check Get All Channel Result
      if l_ChannelCount = Cardinal(l_ChannelLevelList.Count) then
      begin
        // Clear Channnel Level List
        f_ChannelLevelList.Clear;

        // Store Properties
        f_ChannelCount := l_ChannelCount;
        f_MasterLevel := l_MasterLevel;
        f_ChannelLevelList.AddRange(l_ChannelLevelList);
        f_Mute := l_Mute;
        f_Step := l_Step;
        f_StepCount := l_StepCount;
        f_Min := l_Min;
        f_Max := l_Max;
        f_Spin := l_Spin;

        Result := True;
      end;

    finally
      l_ChannelLevelList.Free;
    end;
  end;
end;

// *****************************************************************************
// Changed Value Notify Event
procedure TAudioDevice.OnControlChangeNotify(const a_Data
  : AUDIO_VOLUME_NOTIFICATION_DATA);
var
  l_Data: AUDIO_VOLUME_NOTIFICATION_DATA;
begin;
  l_Data := a_Data;

end;

end.
